let List/map =
        https://prelude.dhall-lang.org/List/map sha256:dd845ffb4568d40327f2a817eb42d1c6138b929ca758d50bc33112ef3c885680
      ? https://prelude.dhall-lang.org/List/map

let Folder =
        ../../Internal/SortingSchema/Folder sha256:da308686c73f4176e1a9968168dc5bfda657a44c981fb18031a04389132e0f19
      ? ../../Internal/SortingSchema/Folder

let template =
        ./Name/template sha256:fae0ecccfdeb9019c905f23fd1d4d358300dd08b45f2039fcb99e30e1250f6a1
      ? ./Name/template

let NameTemplate =
        ../../Internal/NameTemplate/Folder/Type sha256:8f255b7ff5b97857ac9bf8866fd38022f6a76abc9e78bdfa9ce697f194e2ead7
      ? ../../Internal/NameTemplate/Folder/Type

let Predicate =
        ../../Internal/Predicate/Type sha256:f2e4edafdd41ade5527a645f9ae16fb31325419b6bf340c6a81dfa32b60d2462
      ? ../../Internal/Predicate/Type

let text =
        ./Name/Component/text sha256:011fcc0f9c0a7929fe08b606c3bdf0e2859a0554c8ef2f3aa2b44a4bd156255f
      ? ./Name/Component/text

in  λ(name : Text) →
    λ(predicate : Predicate) →
    λ(subfolders : List Folder) →
    λ(Folder : Type) →
    λ ( Fix
      : { getNameF : NameTemplate
        , getPredicateF : Predicate
        , getSubfoldersF : List Folder
        } →
          Folder
      ) →
      Fix
        { getNameF = template [ text name ]
        , getPredicateF = predicate
        , getSubfoldersF =
            List/map Folder@1 Folder (λ(f : Folder@1) → f Folder Fix) subfolders
        }
