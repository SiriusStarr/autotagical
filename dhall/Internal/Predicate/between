let List/map =
        https://prelude.dhall-lang.org/List/map sha256:dd845ffb4568d40327f2a817eb42d1c6138b929ca758d50bc33112ef3c885680
      ? https://prelude.dhall-lang.org/List/map

let Bound =
        ./Bound sha256:5df84f4de5c95a2e6e0cadea46dd4fcf4a0345e2190fe034f064351ede3ec30d
      ? ./Bound

let Predicate =
        ./Type sha256:7bbd5275d9b92c0c5b1c7c0f05885b85796a14c4813d02aae7148a9d418deedf
      ? ./Type

let Tag =
        ../Tag/Type sha256:3791aa7a52b92db3877472c57cec53ca856858bbad81156ef1bf29e9aeb2baee
      ? ../Tag/Type

let TagGroup =
        ../TagGroup/Type sha256:53d772dbc0ab2e509aa6425d923bc93e4fd75854cfdeb9245c37017f6c08bf29
      ? ../TagGroup/Type

in  λ(lb : Bound) →
    λ(ub : Bound) →
    λ(ps : List Predicate) →
    λ(Predicate : Type) →
      let PredicateF =
            < AlwaysF
            | BetweenF :
                { lowerBoundF : Bound
                , upperBoundF : Bound
                , predicatesF : List Predicate
                }
            | HasGroupF : TagGroup
            | HasTagF : { tagF : Tag, withValueF : Optional (List Text) }
            | NotF : Predicate
            >

      in  λ(Fix : PredicateF → Predicate) →
            Fix
              ( PredicateF.BetweenF
                  { lowerBoundF = lb
                  , upperBoundF = ub
                  , predicatesF =
                      List/map
                        Predicate@1
                        Predicate
                        (λ(p : Predicate@1) → p Predicate Fix)
                        ps
                  }
              )
